meme central:memescript/compiler

requires bits, comp_vmemory, io, entries, ometa_base, meme, meme_tr
where
  bits         = central:memescript/bits
  comp_vmemory = central:memescript/comp_vmemory
  io           = central:stdlib/io
  entries      = central:memescript/entries
  ometa_base   = central:memescript/ometa_base
  meme         = central:memescript/meme
  meme_tr      = central:memescript/meme_tr
import OMetaException from ometa_base


MAGIC_NUMBER: 0x420;

class MEC
fields: cmodule, HEADER_SIZE;
init new: fun(cmodule) {
  @cmodule = cmodule;
}
instance_method name_ptr_for: fun(name, mec) {
  var acc = 0;
  var names = mec[:names];
  for (var i = 0; i < names.size; i += 1) {
    var pair = names[i];
    var entry_name_t = pair[0];
    var bsize = pair[1];
    if (entry_name_t == name + "\0") {
      return @HEADER_SIZE + acc;
    }
    acc = acc + bsize;
  }
  Exception.throw("entry " + name.toString + " not found in NAMES");
}
instance_method create_mec_struct: fun(vmem) {
  var mec = {:header: {:magic_number: null,
                      :ot_size: null,
                      :er_size: null,
                      :st_size: null,
                      :names_size: null},
             :names: [],
             :object_table: [],        //the loaded objects of this module
             :external_references: [], //references to core objects
             :symbol_table: [],        //Symbol objects needed
             :reloc_table: []          //addresses within the OT that need relocation on load
    };

  @HEADER_SIZE = mec[:header].size * bits.WSIZE;
  @cmodule.fill(vmem);

  mec[:header][:magic_number] = MAGIC_NUMBER;

  var names_list = vmem.external_names.map(fun(n) { n + "\0" }).unique;

  mec[:names] = names_list.map(fun(name_t) { [name_t, bits.string_block_size(name_t)] });

  mec[:header][:names_size] = mec[:names].map(fun(x) { x[1] }).sum;

  var base = @HEADER_SIZE + mec[:header][:names_size];
  vmem.set_base(base);

  mec[:object_table] = vmem.object_table();

  mec[:header][:ot_size] = mec[:object_table].size;

  var ext_refs = vmem.external_references();
  for (var i = 0; i < ext_refs.size; i += 1) {
    var pair = ext_refs[i];
    mec[:external_references].append(this.name_ptr_for(pair[0], mec));
    mec[:external_references].append(pair[1]);
  }

  mec[:header][:er_size] = mec[:external_references].size * bits.WSIZE;

  var sref = vmem.symbols_references;
  for (i = 0; i < sref.size; i += 1) {
   var p2 = sref[i];
    mec[:symbol_table].append(this.name_ptr_for(p2[0], mec));
    mec[:symbol_table].append(p2[1]);
  }

  mec[:header][:st_size] = mec[:symbol_table].size * bits.WSIZE;

  mec[:reloc_table] = vmem.reloc_table();

  return mec;
}
instance_method dump: fun(filepath) {
  var vmem = comp_vmemory.CompVirtualMemory.new();
  var mec = this.create_mec_struct(vmem);

  io.with_file(filepath.substr(0, filepath.size - 2) + "mec", fun(write) {
    //header
    write(bits.pack(mec[:header][:magic_number]));
    write(bits.pack(mec[:header][:ot_size]));
    write(bits.pack(mec[:header][:er_size]));
    write(bits.pack(mec[:header][:st_size]));
    write(bits.pack(mec[:header][:names_size]));

    //names
    var names = mec[:names];
    for (var i = 0; i < names.size; i += 1) {
      var entry = names[i];
      var name = entry[0];
      var chunk_size = entry[1];
      var text = name + "\0".times(chunk_size - name.size);
      write(text);
    }

    //object table
    var objt = mec[:object_table];
    for (i = 0; i < objt.size; i += 1) {
      write(bits.pack_byte(objt[i]));
    }

    //external references
    var extref = mec[:external_references];
    for (i = 0; i < extref.size; i += 1) {
      write(bits.pack(extref[i]));
    }

    //symbols
    var symt = mec[:symbol_table];
    for (i = 0; i < symt.size; i += 1) {
      write(bits.pack(symt[i]));
    }

    //reloc table
    var relt = mec[:reloc_table];
    for (i = 0; i < relt.size; i += 1) {
      write(bits.pack(relt[i]));
    }
  });
}
end


class Compiler
fields: cmodule, filepath;
instance_method new_module: fun() {
  var module_name = basename(@filepath); //provisory function to get the basename
  @cmodule = entries.CompiledModule.new(module_name);
  return @cmodule;
}
instance_method parse: fun() {
  var ast = ometa_base.parse(io.read_file(@filepath), meme.MemeScriptParser, :start);
  if (ast[0]) {
    //io.print(ast[0]);
    exit(1); //FIXME: we probably want to raise as this might be used as a library
  } else {
    return ast[1];
  }
}
instance_method translate: fun(ast) {
  var input = ometa_base.OMetaStream.with_data([ast]);
  var parser = meme_tr.MemeScriptTranslator.new(this, input);
  parser.prepend_input(:start);
  try {
    parser.apply();
  } catch(OMetaException e) {
    io.print(input.format_error);
    exit(1); //FIXME: we probably want to raise as this might be used as a library
  }
}
instance_method compile: fun(filepath) {
  @filepath = filepath;

  var ast = bench("parsing", fun() {
    this.parse();
  });

  //io.print("translating...");

  bench("translate", fun() {
    this.translate(ast);
  });

  bench("mec dump", fun() {
    var mec = MEC.new(@cmodule);
    mec.dump(filepath);
  });
}
end

main: fun() {
  var args = argv();
  if (args.size < 2) {
    io.print("usage: compiler <path to file.me>");
  } else {
    argv().from(1).each(fun(_, path) {
      io.print(path);
      Compiler.new.compile(path);
   });
  }
}

compile: fun(path) {
  Compiler.new.compile(path);
}
